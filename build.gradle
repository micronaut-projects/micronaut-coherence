buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:3.0.3"
    }
}

subprojects { Project subproject ->
    group "io.micronaut.coherence"

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://s01.oss.sonatype.org/content/repositories/snapshots"
            mavenContent {
                snapshotsOnly()
            }
        }
        maven { url "https://oss.sonatype.org/content/groups/staging" }
        mavenLocal {
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }

    if (subproject.name.endsWith('-bom')) {
        apply plugin: "java-platform"
        version projectVersion

    } else {
        apply plugin: "io.micronaut.build.internal.common"

        dependencies {
            annotationProcessor "io.micronaut:micronaut-inject-java:${micronautVersion}"
            annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:$micronautDocsVersion"

            if(!subproject.name.endsWith('-processor')) {
                compileOnly(platform("com.oracle.coherence.ce:coherence-bom:${coherenceVersion}"))
                testImplementation(platform("com.oracle.coherence.ce:coherence-bom:${coherenceVersion}"))
            }

            testAnnotationProcessor "io.micronaut:micronaut-inject-java:${micronautVersion}"
        }
    }

    apply plugin: "io.micronaut.build.internal.dependency-updates"
    apply plugin: "io.micronaut.build.internal.publishing"
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"
